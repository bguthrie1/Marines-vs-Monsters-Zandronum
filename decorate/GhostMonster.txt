Actor GhostMonster: PlayerPawn
{ 
	// Player monsters start off as ghosts who are given a random weapon that "morphs" into the proper monster class. 

	Player.Displayname "Monster"
//  	Player.SideMove 0 // Don't use, takes too much control from the player.
	// All monsters start off with keys
	Player.StartItem "RedCard"
	Player.StartItem "RedSkull"
	Player.StartItem "BlueCard"
	Player.StartItem "BlueSkull"
	Player.StartItem "YellowCard"
	Player.StartItem "YellowSkull"
	Player.StartItem "SetMonsterClass"
	Player.SoundClass "" // Need to find a way to remove all sounds completley. Player monsters can taunt as a ghost.
  	Player.Face "STR" // Removes the "Doomguy" face. Monsters shouldn't have it anyway
//	 Player.ColorRange 32, 47 // Debating whether to allow players be unique. This would ruin their camouflage with other AI monsters however.
    DamageFactor "Slime", 0 // AI monsters can't get hurt in slime and neither should the player monster.
    -FRIENDLY
    -PICKUP // AI Monsters can't pick up things and neither should the player mosnter.
    +NOSKIN
	+ISMONSTER // So that actors with the "FRIENDLY" flag will attack the player.
    +COUNTKILL // Death by another marine or friendly player counts towards their score.
	+ACTIVATEMCROSS 
//	-SLIDESONWALLS // AI monsters can't wall slide, but this takes too much control from the player.
//	-NOBLOCKMONST // Players would complain that they are stuck when really there is a monster barrier if this flag was removed.
//	-TELESTOMP // Debating whether to allow monster players to telefrag others since monster AI can't normally telefrag. Need additional feedback.

	// Ghost flags to change appearance and walk through walls. This allows the monster players to plan strategies against the marine players.
	-SHOOTABLE
	-SOLID
	+THRUACTORS
//	+NOCLIP // players may spawn in areas they can't get out of
	+NOTARGET
	+NOTRIGGER
	LimitedToTeam 1
	VisibleToTeam 1
	Speed 2
	RenderStyle "Stencil"
	StencilColor "White"
	Alpha 0.6


//	NOTE: To convert monster speeds into player speeds, divide monster speed by 4 and move the decimal to the left.
// There is an issue with the See state in relation to the Baron. It will not trigger for some reason.
// Debating over whether to include paralyzing pain states for player monsters. Leaning towards no.
// The taunts for the monsters sound like marines. This has to be changed.
// Add the sight roar for the imp and baron.
// Cacodemon has issues when respawning

States
  {
	Spawn:
    TNT1 A 0 // Delay frame
	TNT1 A 0 A_JumpIfInventory("NoTargetAlreadySet", 1, 4)
	TNT1 A 0 SetPlayerProperty (0, 1, PROP_NOTARGET)
	TNT1 A 0 Thing_ChangeTID(0, 1)
	TNT1 A 0 A_GiveInventory("NoTargetAlreadySet")
	TNT1 A 0 A_JumpIfInventory("SetAsGhostImp", 1, "ImpGhostIdle")
	TNT1 A 0 A_JumpIfInventory("SetAsGhostBaron", 1, "BaronGhostIdle")
	TNT1 A 0 A_JumpIfInventory("SetAsGhostCacodemon", 1, "CacodemonGhostIdle")
	
	ImpGhostIdle:
	TROO AB 10
	Goto Spawn
	
	BaronGhostIdle:
	BOSS AB 10
	Goto Spawn
	
	CacodemonGhostIdle:
	HEAD A 10
	Goto Spawn
  
	See:
	TNT1 A 0 A_JumpIfInventory("SetAsGhostImp", 1, "ImpGhostMove")
	TNT1 A 0 A_JumpIfInventory("SetAsGhostBaron", 1, "BaronGhostMove")
	TNT1 A 0 A_JumpIfInventory("SetAsGhostCacodemon", 1, "CacodemonGhostMove")
	
	ImpGhostMove:
	TROO AABBCCDD 3
	Goto See
	
	BaronGhostMove:
	BOSS AABBCCDD 3
	Goto See
	
	CacodemonGhostMove:
	HEAD A 3
	Goto See

	Missile:
	TNT1 A 0 A_JumpIfInventory("SetAsGhostImp", 1, "ChangeIntoImp")
	TNT1 A 0 A_JumpIfInventory("SetAsGhostBaron", 1, "ChangeIntoBaron")
	TNT1 A 0 A_JumpIfInventory("SetAsGhostCacodemon", 1, "ChangeIntoCacodemon")
	Goto See
	
	Death:
	TNT1 A 0 A_NoBlocking
	TNT1 A -1 A_ChangeFlag("INVISIBLE",1)
	Stop
	
	ChangeIntoImp:
	TNT1 A 0 A_TakeInventory("SetAsGhostImp")
	TNT1 A 0 A_GiveInventory ("MorphIntoImp")
	Goto See
	
	ChangeIntoBaron:
	TNT1 A 0 A_TakeInventory("SetAsGhostBaron")
	TNT1 A 0 A_GiveInventory ("MorphIntoBaron")
	Goto See
	
	ChangeIntoCacodemon:
	TNT1 A 0 A_TakeInventory("SetAsGhostCacodemon")
	TNT1 A 0 A_GiveInventory ("MorphIntoCacodemon")
	Goto See
  }

}

// Determines the monster class for the ghost player. Stronger monsters will have a lower chance to be selected compared to weaker ones.
actor SetMonsterClass : CustomInventory
{
	States
	{
	Pickup:
	TNT1 A 0 A_Jump(32, 3)
	TNT1 A 0 A_Jump(64, 4)
	TNT1 A 0 A_Jump(128, 5)
	TNT1 A 0 A_GiveInventory("SetAsGhostBaron")
	TNT1 A 0 A_Jump(256, 4)
	TNT1 A 0 A_GiveInventory("SetAsGhostImp")
	TNT1 A 0 A_Jump(256, 2)
	TNT1 A 0 A_GiveInventory("SetAsGhostCacodemon")
	TNT1 A 0
  	Stop
	}
}

Actor MonsterWeapon: Weapon
{
    Weapon.Ammotype "none"
  	Weapon.Ammogive 0
  	Weapon.Ammouse 0
  	Weapon.Selectionorder 0
  	Weapon.SlotNumber 0
}

actor SetAsGhostImp : MonsterWeapon
{
//  +WEAPON.NO_AUTO_SWITCH
//  +WEAPON.CHEATNOTWEAPON
  States
  {
  Ready:
	PUNG A 0 A_Print("You will play as an Imp. Press fire to spawn.", 0, BIGFONT)
	PUNG A 0 A_SetBlend ("0, 0, 50", .5, 5)
    PUNG A 1 A_WeaponReady
    loop
  Deselect:
    PUNG A 1 A_Lower
    loop
  Select:
    PUNG A 1 A_Raise
    loop
  Fire:
    goto Ready
  }
}

actor MorphIntoImp : PowerupGiver
{
	powerup.duration 0x7FFFFFFD
	Powerup.Type PowerImpMorph
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
}

actor PowerImpMorph : PowerMorph
{
	PowerMorph.PlayerClass "ImpPlayer"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYTOMEOFPOWER)
	PowerMorph.MorphFlash "none"
	PowerMorph.UnMorphFlash "none"
}

Actor SetAsGhostBaron : MonsterWeapon
{
//  +WEAPON.NO_AUTO_SWITCH
//  +WEAPON.CHEATNOTWEAPON
  States
  {
  Ready:
	PUNG A 0 A_Print("You will play as a Baron Of Hell. Press fire to spawn.", 0, BIGFONT)
	PUNG A 0 A_SetBlend ("0, 0, 50", .5, 5)
    PUNG A 1 A_WeaponReady
    loop
  Deselect:
    PUNG A 1 A_Lower
    loop
  Select:
    PUNG A 1 A_Raise
    loop
  Fire:
    goto Ready
  }
}

actor MorphIntoBaron : PowerupGiver
{
	powerup.duration 0x7FFFFFFD
	Powerup.Type PowerBaronMorph
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
}

actor PowerBaronMorph : PowerMorph
{
	PowerMorph.PlayerClass "BaronPlayer"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYTOMEOFPOWER)
	PowerMorph.MorphFlash "none"
	PowerMorph.UnMorphFlash "none"
}

actor SetAsGhostCacodemon : MonsterWeapon
{
//  +WEAPON.NO_AUTO_SWITCH
//  +WEAPON.CHEATNOTWEAPON
  States
  {
  Ready:
	TNT1 A 0 A_Print("You will play as a Cacodemon. Press fire to spawn.", 0, BIGFONT)
	TNT1 A 0 A_SetBlend ("0, 0, 50", .5, 5)
    TNT1 A 1 A_WeaponReady
    loop
  Deselect:
    TNT1 A 1 A_Lower
    loop
  Select:
    TNT1 A 1 A_Raise
    loop
  Fire:
    goto Ready
  }
}

actor MorphIntoCacodemon : PowerupGiver
{
	powerup.duration 0x7FFFFFFD
	Powerup.Type PowerCacodemonMorph
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
}

actor PowerCacodemonMorph : PowerMorph
{
	PowerMorph.PlayerClass "CacodemonPlayer"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYTOMEOFPOWER)
	PowerMorph.MorphFlash "none"
	PowerMorph.UnMorphFlash "none"
}

actor NoTargetAlreadySet : Inventory
{
	Game Doom
	Inventory.MaxAmount 1
	Inventory.Amount 1
}