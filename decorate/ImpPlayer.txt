actor DoomImpBall2 : MonsterWeapon
{
//  +WEAPON.NOAUTOFIRE
//  +WEAPON.CHEATNOTWEAPON
  obituary "%o was slashed by %k."
  States
  {
  Ready:
    PUNG A 1 A_WeaponReady
    loop
  Deselect:
    PUNG A 1 A_Lower
    loop
  Select:
    PUNG A 0 A_Raise // Additional A_Raises are needed for a quicker recovery from a pain state (matches that of normal monsters).
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 0 A_Raise
    PUNG A 1 A_Raise
    loop
  Fire:
    PUNG B 8
    PUNG C 8 A_JumpIfCloser(75,"MeleeAttack")
    PUNG D 6 A_FireCustomMissile("DoomImpBall")
    PUNG C 6
    PUNG B 15 A_ReFire
    goto Ready
  MeleeAttack:
    PUNG D 0 A_PlaySound("imp/melee")
    PUNG D 8 A_CustomPunch(3)
    PUNG C 6
    PUNG B 15 A_ReFire
    goto Ready
  }
  Obituary "%o got burned by %k."
}


actor ImpPlayer : GhostMonster
{
  Player.MorphWeapon "DoomImpBall2"
  Player.Viewheight 48
  Player.SoundClass "imp2" // Custom sound classes used primarly for *usefail and *grunt.
  Obituary "%o was burned by %k."
  Health 60
  Radius 20
  Height 56
  Mass 100
  Speed 0.2
  Painchance 200
  DamageFactor "DoomImpFire", 0
  +FLOORCLIP

  // Remove flags and properties inherited from being a ghost.
  +SHOOTABLE
  +SOLID
  -THRUACTORS
  -NOTRIGGER
  -NOTARGET
  RenderStyle "Normal"
  Alpha 1.0

  states
  {
  Spawn:
    TROO AB 10
    TROO A 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto Spawn
    TROO A 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Actors with the friendly flag in this case are actual enemies.
    Goto Spawn
    TROO A 0 A_Jump (30, "PlaySightRoar") // If the player is looking at an enemy, roar at the enemy, but not all the time.
    loop
  See:
    TROO AABBCCDD 3
    TROO D 0 A_Jump (20, "PlayActiveRoar")
    TROO D 0 A_JumpIfTargetInLOS(1, 90, JLOSF_DEADNOJUMP, 0, 0)
    Goto See
    TROO D 0 A_CheckFlag("FRIENDLY", 1, AAPTR_PLAYER_GETTARGET) // Jump if the target is has the friendly flag (an actual enemy in this case).
    Goto See
    TROO D 0 A_Jump (30, "PlaySightRoar") // If the player is chasing an enemy, roar at the enemy, but not all the time.
    loop
  Melee:
  Missile:
    TROO EF 8
    TROO G 6
    TROO A 0 
    goto Spawn
  Pain:
    TROO H 2
    TROO H 2 A_PlaySound("imp/pain")
    TROO A 0 A_GiveInventory ("DoomImpBall2", 1)
    TROO A 0 A_SelectWeapon ("DoomImpBall2")
    goto Spawn
  Death:
    TROO A 0 A_TakeInventory("DoomImpBall2")
	TROO A 0 A_GiveInventory("SetMonsterClass")
    TROO I 8 
    TROO I 0 A_Playsound ("imp/death")
    TROO J 8
    TROO J 0
    TROO K 6
    TROO L 6 A_NoBlocking
    TROO M -1
    stop
  XDeath:
    TROO A 0 A_TakeInventory("DoomImpBall2")
	TROO A 0 A_GiveInventory("SetMonsterClass")
    TROO N 5 
    TROO O 5 A_XScream
    TROO P 5
    TROO Q 5 A_NoBlocking
    TROO RST 5
    TROO U -1
    stop
  Raise:
    TROO ML 8
    TROO KJI 6
    goto Spawn

  PlayActiveRoar:
    TROO A 0 A_PlaySound("imp/active")
    Goto See

  PlaySightRoar:
    TROO A 0 A_PlaySound("imp/sight") // Rawr.
    Goto Spawn
  }
}




actor ImpGhostItem : PowerupGiver
{
	powerup.duration 999999999
	Powerup.Type ImpGhostMorph
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
}



actor PowerImpGhostMorph : PowerMorph
{
	PowerMorph.PlayerClass "ImpGhost"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYTOMEOFPOWER)
	PowerMorph.MorphFlash "none"
	PowerMorph.UnMorphFlash "none"
}
